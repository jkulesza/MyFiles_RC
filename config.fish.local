# Unset fish shell greeting.
set fish_greeting

# Set default editor.
alias vi='vim'
set -gx EDITOR vim

# Set global prompt color preferences.
set -Ux c_host (set_color black; set_color -b 666666)
set -Ux c_gray (set_color -b normal; set_color 666666)
set -Ux c_normal (set_color -b normal; set_color normal)

# Setup vi-like behavior, with some tweaks.
fish_vi_key_bindings

# Let control-C cancel the current command.
# with modification from: https://github.com/fish-shell/fish-shell/issues/2077
function fish_vi_key_bindings_local
  fish_vi_key_bindings
  bind -e -M insert \cc
  bind -M insert -m insert \cc 'commandline ""'
end
set -g fish_key_bindings fish_vi_key_bindings_local
# Preserve default autocompletion behavior.
# from: https://github.com/fish-shell/fish-shell/issues/3541
function fish_user_key_bindings
  for mode in insert default visual
    bind -M $mode \cf forward-char
  end
end

# Don't show vi mode status.
# from: https://github.com/fish-shell/fish-shell/issues/3232
function fish_mode_prompt
  # NOOP - Disable vim mode indicator
end

# Customize left-leaning prompt.
function fish_prompt
  # Determine hostname once to save a few cycles each time the prompt is drawn.
  if not set -q __fish_prompt_hostname
    set -g __fish_prompt_hostname (hostname | cut -d . -f 1)
  end

  # Capture and process variables of interest.
  set gitprompt (__fish_git_prompt | sed -e "s|^ (||" | sed -e "s|)||")
  set longpwd (echo $PWD | sed -e "s|^$HOME|~|" -e 's|^/private||' )

  # Print git information, if in a git repo.
  if test -n "$gitprompt"
    echo "$c_gray""[GIT("$gitprompt")]""$c_normal"
  end
  echo (whoami)"@$c_host"$__fish_prompt_hostname"$c_normal""["$longpwd"]> "
end

set -U fish_user_paths /local/brew/bin $fish_user_paths
set -U fish_user_paths /local/brew/opt/python/libexec/bin/ $fish_user_paths
set -U fish_user_paths /local/brew/opt/sphinx-doc/bin $fish_user_paths

alias g='git'
alias la='ls -al'

